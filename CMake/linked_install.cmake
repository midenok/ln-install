macro(pop list out)
    list(GET ${list} 0 ${out}) 
    list(REMOVE_AT ${list} 0)
endmacro()

macro(push list in)
    list(APPEND ${list} ${in}) 
endmacro()

macro(join list delim out)
    set(sep "")
    foreach(arg ${${list}})
        set(${out} "${${out}}${sep}${arg}")
        set(sep ${delim})
    endforeach()
endmacro()

macro(symlink src dst rename)
    if("${rename}" STREQUAL "")
        get_filename_component(file "${src}" NAME)
    else()
        set(file "${rename}")
    endif()
    # message("symlink src:${src} dst:${dst} file:${file}")
    set(dst_file "${dst}/${file}")
    if(NOT IS_DIRECTORY "${dst}")
        execute_process(
            COMMAND mkdir -p "${dst}"
            RESULT_VARIABLE rc)
        if(rc)
            message(FATAL_ERROR "Failed mkdir -p ${dst}")
        endif()
    elseif(IS_SYMLINK "${dst_file}")
        file(REMOVE "${dst_file}")
    endif()
    message("-- Symlinking: ${dst_file}")
    execute_process(
        COMMAND cmake -E create_symlink "${src}" "${dst_file}"
        RESULT_VARIABLE rc)
    if(rc)
        message(FATAL_ERROR "create_symlink failed! src:${src} dst:${dst} dst_file:${dst_file}")
    endif()
endmacro()

macro(symlink_dir src dst rename)
    string(REGEX MATCH "/[.]?$" last_char "${src}")
    if (NOT "${last_char}" STREQUAL "")
        file(GLOB files "${src}*")
        foreach(file ${files})
            # message("symlink ${file} ${dst} ${rename}")
            symlink("${file}" "${dst}" "${rename}")
        endforeach()
    else()
        # message("symlink ${src} ${dst} ${rename}")
        symlink("${src}" "${dst}" "${rename}")
    endif()
endmacro()

function(FILE _CMD)
    if(_CMD STREQUAL INSTALL)
        set(_args ${ARGN})
        set(_cmd FILES)
        set(_type FILE)
        # join(_args " " trace)
        # message("INSTALL " ${trace})
        while (_args)
            pop(_args _arg)
            if(_arg STREQUAL DESTINATION)
                set(_cmd ${_arg})
                pop(_args _dest)
            elseif(_arg STREQUAL FILES OR _arg STREQUAL PROGRAMS)
                set(_cmd FILES)
                pop(_args _files)
            elseif(_arg STREQUAL TYPE)
                set(_cmd ${_arg})
                pop(_args _type)
            elseif(_arg STREQUAL RENAME)
                set(_cmd ${_arg})
                pop(_args _rename)
            elseif(_arg STREQUAL FILE_PERMISSIONS OR
                    _arg STREQUAL DIRECTORY_PERMISSIONS OR
                    _arg STREQUAL NO_SOURCE_PERMISSIONS OR
                    _arg STREQUAL USE_SOURCE_PERMISSIONS OR
                    _arg STREQUAL FILES_MATCHING OR
                    _arg STREQUAL PATTERN OR
                    _arg STREQUAL REGEX OR
                    _arg STREQUAL EXCLUDE OR
                    _arg STREQUAL PERMISSIONS OR
                    _arg STREQUAL CONFIGURATIONS OR
                    _arg STREQUAL COMPONENT OR
                    _arg STREQUAL OPTIONAL OR
                    _arg STREQUAL MESSAGE_NEVER OR
                    _arg STREQUAL EXCLUDE_FROM_ALL)
                set(_cmd ${_arg})
            else()
                if(_cmd STREQUAL FILES)
                    push(_files "${_arg}")
                endif()
            endif()
        endwhile()
        # message("dest: " ${dest} "; files: " ${files})
        foreach(_file ${_files})
            if(_type STREQUAL DIRECTORY)
                # https://cmake.org/cmake/help/v3.7/command/install.html#installing-directories
                # message(${ARGV})
                symlink_dir("${_file}" "${_dest}" "${_rename}")
            else()
                symlink("${_file}" "${_dest}" "${_rename}")
            endif()
        endforeach()
    else()
        # message(${ARGV})
        get_cmake_property(_vars_before VARIABLES)
        _FILE(${ARGV})
        get_cmake_property(_vars_after VARIABLES)
        list(REMOVE_ITEM _vars_after _vars_before ${_vars_before})
        foreach (_var ${_vars_after})
            set(${_var} ${${_var}} PARENT_SCOPE)
        endforeach()
    endif()
endfunction(FILE)
